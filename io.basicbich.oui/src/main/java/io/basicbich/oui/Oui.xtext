grammar io.basicbich.oui.Oui with org.eclipse.xtext.common.Terminals

generate oui "http://basicbich.io/oui/Oui"

Program: {Program} (first=InstructionSet)?;

InstructionSet: declarations+=Declaration* instruction=Instruction ('|' next=InstructionSet)?;

/* Declaration */
Declaration: name=ID '=' value=Instruction ';';

/* Instruction */
Instruction: Selector | Object | '(' InstructionSet ')';

// Selector
Selector: scope=SelectorScope (fragments+=SelectorFragment)*;

SelectorScope: RootSelectorScope | NamedSelectorScope;
RootSelectorScope: {RootSelectorScope} '$';
NamedSelectorScope: DeclarationSelectorScope | FilterSelectorScope;
DeclarationSelectorScope hidden(): '$' declaration=[Declaration];
FilterSelectorScope: filter=Filter;

SelectorFragment: NamedSelectorFragment | IndexSelectorFragment | SliceSelectorFragment | StartSliceSelectorFragment;
NamedSelectorFragment: PropertySelectorFragment;
PropertySelectorFragment hidden(): '.' key=ObjectPropertyKey;

IndexSelectorFragment: {IndexSelectorFragment} '[' indexes+=INT? (',' indexes+=INT)* ']';
SliceSelectorFragment: '[' start=INT? ':' end=INT ']';
StartSliceSelectorFragment: '[' start=INT ':' ']';

// Object
Object: {Object} '{' (properties+=ObjectProperty (',' properties+=ObjectProperty)*)? '}';
ObjectProperty: (key=ObjectPropertyKey ':')? value=Instruction;
ObjectPropertyKey: ID | STRING;

/* Filter */
Filter: name=ID;
