grammar io.basicbich.oui.Oui with org.eclipse.xtext.common.Terminals

generate oui "http://basicbich.io/oui/Oui"

Program: {Program} (instructions=InstructionSet)?;

InstructionSet: instructions+=Instruction ('|' instructions+=Instruction)*;
Instruction: Selector | ObjectConstructor | Assignment | '(' InstructionSet ')';

/* Selector */
Selector: scope=SelectorScope (fragments+=SelectorFragment)*;

SelectorScope: RootSelectorScope | NamedSelectorScope;
NamedSelectorScope: AssignmentSelectorScope | FilterSelectorScope;
RootSelectorScope: {RootSelectorScope} '$';
AssignmentSelectorScope: '$' assignment=[Assignment]; // TODO: Add scope
FilterSelectorScope: filter=Filter;

SelectorFragment: NamedSelectorFragment | IndexSelectorFragment | SliceSelectorFragment | StartSliceSelectorFragment;
NamedSelectorFragment: AttributeSelectorFragment;
AttributeSelectorFragment: '.' attribute=ObjectAttributeName;

IndexSelectorFragment: {IndexSelectorFragment} '[' indexes+=INT? (',' indexes+=INT)* ']';
SliceSelectorFragment: '[' start=INT? ':' end=INT ']';
StartSliceSelectorFragment: '[' start=INT ':' ']';

/* Object constructor */
ObjectConstructor: '{' attributes+=ObjectAttribute (',' attributes+=ObjectAttribute)* '}';
ObjectAttribute: (name=ObjectAttributeName ':')? selector=Selector;
ObjectAttributeName: ID;

/* Assignment */
Assignment: name=ID '=' instruction=Instruction; // TODO: validate that name does not exist

/* Filter */
Filter: name=ID;
